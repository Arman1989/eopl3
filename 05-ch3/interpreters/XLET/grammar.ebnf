Program ::= Expr
Expr    ::= Const | Var | Diff | Zero | If | Let
          | Minus
          | Add | Mul | Div
          | Equal | Greater | Less
Const   ::= Number
Var     ::= Id
Diff    ::= '-' '(' Expr ',' Expr ')'
Zero    ::= 'zero?' '(' Expr ')'
If      ::= 'if' Expr 'then' Expr 'else' Expr
Let     ::= 'let' Id '=' Expr 'in' Expr
Minus   ::= 'minus' '(' Expr ')'
Add     ::= 'add' '(' Expr ',' Expr ')'
Mul     ::= 'mul' '(' Expr ',' Expr ')'
Div     ::= 'div' '(' Expr ',' Expr ')'
Equal   ::= 'equal?' '(' Expr ',' Expr ')'
Greater ::= 'greater?' '(' Expr ',' Expr ')'
Less    ::= 'less?' '(' Expr ',' Expr ')'
Number  ::= [0-9]+
Id      ::= [a-z]+

/* Use https://www.bottlecaps.de/rr/ui to view it as a Railroad Diagram. */
