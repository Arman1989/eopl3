Program   ::= Expr
Expr      ::= Const | Var
            | Diff | Minus | Add | Mult | Div
            | Zero | Equal | Greater | Less
            | Cons | Car | Cdr | Null | EmptyList
            | If
            | Let
Const     ::= Number
Var       ::= Id
Diff      ::= '-' '(' Expr ',' Expr ')'
Minus     ::= 'minus' '(' Expr ')'
Add       ::= 'add' '(' Expr ',' Expr ')'
Mult      ::= 'mult' '(' Expr ',' Expr ')'
Div       ::= 'div' '(' Expr ',' Expr ')'
Zero      ::= 'zero?' '(' Expr ')'
Equal     ::= 'equal?' '(' Expr ',' Expr ')'
Greater   ::= 'greater?' '(' Expr ',' Expr ')'
Less      ::= 'less?' '(' Expr ',' Expr ')'
Cons      ::= 'cons' '(' Expr ',' Expr ')'
Car       ::= 'car' '(' Expr ')'
Cdr       ::= 'cdr' '(' Expr ')'
Null      ::= 'null?' '(' Expr ')'
EmptyList ::= 'emptylist'
If        ::= 'if' Expr 'then' Expr 'else' Expr
Let       ::= 'let' Id '=' Expr 'in' Expr
Number    ::= '-'? [0-9]+
Id        ::= [a-z]+
