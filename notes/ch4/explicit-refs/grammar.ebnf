Program ::= Expr

Expr    ::= Const | Var
          | Begin
          | Diff
          | Zero | If
          | Let | Letrec
          | Proc | Call
          | Newref | Deref | Setref

Const   ::= Number
Var     ::= Id

Begin   ::= 'begin' Expr (';' Expr)* 'end'

Diff    ::= '-' '(' Expr ',' Expr ')'

Zero    ::= 'zero?' '(' Expr ')'
If      ::= 'if' Expr 'then' Expr 'else' Expr

Let     ::= 'let' Id '=' Expr 'in' Expr
Letrec  ::= 'letrec' Decl+ 'in' Expr
Decl    ::= Id '(' Id ')' '=' Expr

Proc    ::= 'proc' '(' Id ')' Expr
Call    ::= '(' Expr Expr ')'

Newref  ::= 'newref' '(' Expr ')'
Deref   ::= 'deref' '(' Expr ')'
Setref  ::= 'setref' '(' Expr ',' Expr ')'

Number  ::= '-'? [0-9]+
Id      ::= [a-z]+
